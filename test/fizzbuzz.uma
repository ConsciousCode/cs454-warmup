ldi 0 0
ldi 1 1
ldi 2 1 ; i

label @main:loop
label @check_15
    ldi 3 15
    div 4 2 3
    mul 4 4 3 ;; r4 = r2 / 15 * 15

    nan 4 4 4
    add 4 4 1
    add 4 2 4 ;; r4 = r2 - r4

    ldi 5 @print_fizzbuzz
    ldi 6 @check_3
    mov 5 6 4
    prg 0 5 ;; goto (r4? r6 : r5)

label @print_fizzbuzz
    ldi 3 'F'
    out 3
    ldi 3 'i'
    out 3
    ldi 3 'z'
    out 3
    out 3
    ldi 4 'B'
    out 4
    ldi 4 'u'
    out 4
    out 3
    out 3

    ldi 5 @main:continue
    prg 0 5

label @check_3
    ldi 3 3
    div 4 2 3
    mul 4 4 3 ;; r4 = r2 / 3 * 3

    nan 4 4 4
    add 4 4 1
    add 4 2 4 ;; r4 = r2 - r4

    ldi 5 @print_fizz
    ldi 6 @check_5
    mov 5 6 4
    prg 0 5 ;; goto (r4? r6 : r5)

label @print_fizz
    ldi 3 'F'
    out 3
    ldi 3 'i'
    out 3
    ldi 3 'z'
    out 3
    out 3

    ldi 5 @main:continue
    prg 0 5

label @check_5
    ldi 3 5
    div 4 2 3
    mul 4 4 3 ;; r4 = r2 / 5 * 5

    nan 4 4 4
    add 4 4 1
    add 4 2 4 ;; r4 = r2 - r4

    ldi 5 @print_buzz
    ldi 6 @print_num
    mov 5 6 4
    prg 0 5 ;; goto (r4? r6 : r5)

label @print_buzz
    ldi 3 'B'
    out 3
    ldi 3 'u'
    out 3
    ldi 3 'z'
    out 3
    out 3

    ldi 5 @main:continue
    prg 0 5

label @print_num
    mov 3 2 1 ;; r3 = i
    ldi 7 0 ; Linked list of cons cells, (car . cdr)

;;; Unpack the integer into a reversed linked list of digits
label @print_num:digitize
    ldi 5 10
    div 4 3 5
    mul 4 4 5 ;; r4 = r2 / 10 * 10

    nan 4 4 4
    add 4 4 1
    add 4 3 4 ;; r4 = r3 - r4

    ldi 6 2
    new 6 6
    sta 6 0 4
    sta 6 1 7
    mov 7 6 1 ;; r7 = (r4 . r7)

    div 3 3 5 ;; r3 = r3 / 10

    ldi 5 @print_num:print
    ldi 6 @print_num:digitize
    mov 5 6 3
    prg 0 5

;;; Pop digits off the list to print them in-order
label @print_num:print
    lda 4 7 0
    ldi 3 '0'
    add 4 4 3
    out 4 ;; putc(car r7 + '0')

    lda 4 7 1
    del 7
    mov 7 4 1 ;; r7 = pop()
    
    ldi 5 @main:continue
    ldi 6 @print_num:print
    mov 5 6 4
    prg 0 5

label @main:continue
    ldi 4 0x0A
    out 4

    add 2 2 1 ;; i = i + 1

    ldi 3 101
    nan 3 3 3
    add 3 3 1
    add 3 2 3 ;; r3 = i - 101

    ldi 5 @main:break
    ldi 6 @main:loop
    mov 5 6 3
    prg 0 5 ;; if(i == 101) break

label @main:break
    hlt